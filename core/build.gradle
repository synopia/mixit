apply plugin: "java"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
    appName = "audio"
    gdxVersion = '1.7.0'
    roboVMVersion = '1.8.0'
    box2DLightsVersion = '1.4'
    ashleyVersion = '1.6.0'
    aiVersion = '1.6.0'
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    processors
}
dependencies {
    processors project(":shadow-objects:annotation-processor")
    compile project(":shadow-objects:core")
    compile files('../tarsos-dsp/build/TarsosDSP-2.3-bin.jar')
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'org.apache.logging.log4j:log4j-core:2.3'
    compile 'org.apache.logging.log4j:log4j-api:2.3'
    compile 'org.apache.logging.log4j:log4j-jul:2.3'

    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.kotcrab.vis:vis-ui:0.8.1"

    compile 'com.mpatric:mp3agic:0.8.3'
}
task generateProcs(type: JavaCompile, group: 'build',) {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.processors;
    options.compilerArgs = [
            "-proc:only",
            "-processor", "org.synopia.shadow.InterfaceProcessor",
            "-ApackageName=org.synopia.audio.model",
            "-Aprefix=A",
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next();
}
compileJava {
    dependsOn generateProcs
    source generateProcs.destinationDir
}

